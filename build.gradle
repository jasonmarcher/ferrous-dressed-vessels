buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.apache.commons:commons-csv:1.6"
    }
}

import groovy.json.JsonSlurper
import java.io.FileWriter
import java.nio.charset.Charset
import java.time.LocalDate
import java.time.format.DateTimeFormatter
import org.apache.commons.csv.CSVFormat
import org.apache.commons.csv.CSVParser
import org.apache.commons.csv.CSVPrinter
import org.ships.gradle.tasks.GenerateGunDataset

task generateGunData(type: GenerateGunDataset) {}

task generateClassData(type: Copy) {
    into 'build'
    from 'res/prebuilt/classes.csv'
}

task generatePrototypeData(type: Copy) {
    into 'build'
    from 'res/prebuilt'
    include 'batteries.csv'
    include 'fitting-battery.csv'
    include 'gun-mounts.csv'
    include 'gun.csv'
    include 'launcher-mount.csv'
}

task generateShipData(type: Copy) {
    into 'build'
    from 'res/prebuilt'
    include 'service.csv'
}

task generateShipHistory {
    dependsOn generateShipData

    doLast {
        file(buildDir).mkdirs()
        def today = LocalDate.now().format(DateTimeFormatter.ISO_LOCAL_DATE)

        def printer = new CSVPrinter(new FileWriter("$buildDir/history.csv"), CSVFormat.EXCEL)
        printer.printRecord('ClassID','ShipName','State','Date')

        // Service
        def records = CSVParser.parse(file("$buildDir/service.csv"), Charset.forName('UTF-8'), CSVFormat.EXCEL.withFirstRecordAsHeader())
        records.forEach { record ->
            if (record.get("ShipName")) {
                if (!record.get("Start")) {throw new GradleException("Ship '${record.get("ShipName")}' is missing a Start date for service history.")}
                def currentDate = LocalDate.parse(record.get("Start"))
                currentDate = currentDate.minusDays(currentDate.getDayOfMonth() - 1)
                def endDate = LocalDate.parse(record.get("End") ?: today)
                endDate = endDate.minusDays(endDate.getDayOfMonth() - 1)
                
                while (currentDate.isBefore(endDate)) {
                    printer.print(record.get('ClassID'))
                    printer.print(record.get('ShipName'))
                    printer.print(record.get('State'))
                    printer.print(currentDate)
                    printer.println()
                    currentDate = currentDate.plusMonths(1)
                }
            }
        }

        printer.close(true)
    }
}

task clean(type: Delete) {
    delete 'build'
}

task build {
    dependsOn generateGunData, generateClassData, generateShipHistory, generatePrototypeData
}